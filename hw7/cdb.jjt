options {
    JJTREE_OUTPUT_DIRECTORY="src/wci/frontend";
    NODE_EXTENDS="wci.intermediate.icodeimpl.ICodeNodeImpl";
    IGNORE_CASE=true;
    MULTI=true;
    VISITOR=true;
}

PARSER_BEGIN(CDB)
package wci.frontend;

import java.util.ArrayList;
import java.io.*;

import wci.intermediate.*;
import wci.intermediate.symtabimpl.*;
import wci.backend.*;
import wci.util.*;

import static wci.intermediate.symtabimpl.SymTabKeyImpl.*;
import static wci.intermediate.symtabimpl.DefinitionImpl.*;
import static wci.intermediate.symtabimpl.RoutineCodeImpl.*;
import static wci.intermediate.symtabimpl.SymTabKeyImpl.*;
import static wci.intermediate.icodeimpl.ICodeKeyImpl.*;

public class CDB 
{
	private static final String SOURCE_SUFFIX = ".cdb";
    private static final String OUTPUT_SUFFIX = ".j";

	private static SymTabStack symTabStack;
    private static SymTabEntry programId;

  public static void main(String[] args)
  	throws Exception
  {
	// Create and initialize the symbol table stack.
    symTabStack = SymTabFactory.createSymTabStack();
    Predefined.initialize(symTabStack);

	// Process the source file path which ends in .cdb
    // and create the output file path which ends in .j
	String sourceFilePath = args[0];
    int truncatedLength = sourceFilePath.length() - SOURCE_SUFFIX.length();
    int suffixIndex = sourceFilePath.lastIndexOf(SOURCE_SUFFIX);
    String objectFilePath = (suffixIndex == truncatedLength)
        ? sourceFilePath.substring(0, truncatedLength) + OUTPUT_SUFFIX
        : sourceFilePath + OUTPUT_SUFFIX;

	// Parse the CDB program.
    Reader reader = new FileReader(sourceFilePath);
    CDB parser = new CDB(reader);
    SimpleNode rootNode = parser.program();

    // Print the cross-reference table.
    CrossReferencer crossReferencer = new CrossReferencer();
    crossReferencer.print(symTabStack);

	// Visit the parse tree nodes to decorate them with type information.
    TypeSetterVisitor typeVisitor = new TypeSetterVisitor();
    rootNode.jjtAccept(typeVisitor, null);

	// Create and initialize the ICode wrapper for the parse tree.
    ICode iCode = ICodeFactory.createICode();
    iCode.setRoot(rootNode);
    programId.setAttribute(ROUTINE_ICODE, iCode);

    // Print the parse tree.
    ParseTreePrinter treePrinter = new ParseTreePrinter(System.out);
    treePrinter.print(symTabStack);
  }
}
PARSER_END(CDB)

SKIP: {
    " " | "\t"
}

SKIP:
{
  <SINGLE_LINE_COMMENT : "//" (~["\n","\r"])* ("\r" | "\n" | "\r\n") >
} 

//skip multi-line comments
MORE : {
  "/*" : IN_MULTI_LINE_COMMENT
}

<IN_MULTI_LINE_COMMENT>
MORE : {
  < ~[] >
}

<IN_MULTI_LINE_COMMENT>
SPECIAL_TOKEN : {
  <MULTI_LINE_COMMENT: "*/"> : DEFAULT
}

TOKEN: { /* Reserved words */
    <IF        : "IF">
  | <ELSE      : "ELSE">
  | <TRUE      : "TRUE">
  | <FALSE     : "FALSE">
  | <WHILE     : "WHILE">
  | <FOR       : "FOR">
  | <DO        : "DO">
  | <STRING    : "STRING">
  | <INT       : "INT">
  | <REAL      : "REAL">
  | <BOOLEAN   : "BOOLEAN">
}

TOKEN: /* Special symbols */
{
    <COLON        : ":">
  | <AT           : "@">
  | <EQUALS       : "=">
  | <EE           : "==">
  | <NE           : "!=">
  | <LT           : "<">
  | <LE           : "<=">
  | <GT           : ">">
  | <GE           : ">=">
  | <COMMA        : ",">
  | <DOT          : ".">
  | <SEMICOLON    : ";">
  | <PLUS         : "+">
  | <MINUS        : "-">
  | <STAR         : "*">
  | <SLASH        : "/">
  | <QUOTE        : "'">
  | <DOUBLE_QUOTE : "\"">
  | <LEFT_PAREN   : "(">
  | <RIGHT_PAREN  : ")">
  | <LEFT_BRACE   : "{">
  | <RIGHT_BRACE  : "}">   
  | <AND          : "&&">
  | <OR           : "||">
}

TOKEN: { /* Identifiers, strings, and numbers */
    <IDENTIFIER : <LETTER>(<LETTER>|<DIGIT>)*>
  | <STRING_LIT : <DOUBLE_QUOTE>(~["\""])*<DOUBLE_QUOTE>>

  | <INT_LIT    : <UNSIGNED_NUMBER>>
  | <REAL_LIT   : <WHOLE><FRACTION><EXPONENT>
             | <WHOLE><FRACTION>
             | <WHOLE><EXPONENT>>

  | <#LETTER : ["a"-"z", "A"-"Z"]>
  | <#DIGIT  : ["0"-"9"]>
  | <#SIGN   : ["+", "-"]>

  | <#UNSIGNED_NUMBER : (<DIGIT>)+>
  | <#WHOLE           : <UNSIGNED_NUMBER>>
  | <#FRACTION        : "." <UNSIGNED_NUMBER>>
  | <#EXPONENT        : "E"(<SIGN>)?<UNSIGNED_NUMBER>>
}

TOKEN: { /*	Error token */
    <EOL     : "\r" | "\n" | "\r\n">
 |  <ERROR : ~["\r", "\n"]>
}

SimpleNode program() : {}
{
  (<EOL>)*
  (
    try {
        statement() {System.out.println("Processed Statement");}
      | error()     {handleError(token);}
    }
    catch (ParseException ex) {
      handleError(ex.currentToken);
    }
  )+
  <EOF>
  {return jjtThis;}
}

SimpleNode compoundStatement() : {}
{    <LEFT_BRACE>
	(<EOL>)*    (statement())+
    <RIGHT_BRACE>
	(<EOL>)*
    { return jjtThis; }
}

void statement() #void : {}
{
  (       controlStatement()
     | declarationStatement()
     | assignmentStatement()
  )
}

void controlStatement() #void : {}
{
  (
	  whileLoop()
	| forLoop() 
	| doLoop() (<EOL>)*
    | ifStatement()
  )
}

void whileLoop() : {}{  <WHILE> <LEFT_PAREN> expression() <RIGHT_PAREN> 
	(<EOL>)*
  (
	  compoundStatement() 
	| statement()
  )
}

void forLoop() : {}
{  <FOR>
  <LEFT_PAREN>
  (assignment())?
  <SEMICOLON>
  (expression())?
  <SEMICOLON>
  (assignment())?
  <RIGHT_PAREN>
  (<EOL>)*
  (
	  compoundStatement()
	| statement()
  )
}

void doLoop() : {}
{	<DO>
	(<EOL>)*
	(
	  compoundStatement()
	| statement()
    )
    <WHILE> <LEFT_PAREN> expression() <RIGHT_PAREN> <SEMICOLON>
}

void ifStatement() : {}
{	<IF> <LEFT_PAREN> expression() <RIGHT_PAREN>
	(<EOL>)*
	(	  compoundStatement()
	| statement()
    )
    ( LOOKAHEAD(1) <ELSE> (
		  compoundStatement()
		| statement()
    ))?
}

void declarationStatement() #void : {}
{  <AT> declaration() <SEMICOLON> (<EOL>)*
}

void declaration() #void : {ArrayList<SymTabEntry> variableList;
	                                 int index = 0;}
{
  <IDENTIFIER> {
    variableList = new ArrayList<SymTabEntry>();
    SymTabEntry typeId = symTabStack.lookup(token.image);
    typeId.appendLineNumber(token.beginLine);
    TypeSpec type = typeId.getTypeSpec();
  }
	<IDENTIFIER> {
	        processVariableDecl(token, index++, variableList);
	}
	(
	    <COMMA>
	    <IDENTIFIER> {
	        processVariableDecl(token, index++, variableList);
	    }
	)*
	{
	    for (SymTabEntry variableId : variableList) {
	        variableId.setTypeSpec(type);
		}	}
}

void assignmentStatement() : {}
{	assignment() <SEMICOLON> (<EOL>)*
}

void assignment() : {}
{
    variable() {
        SymTabEntry variableId = symTabStack.lookup(token.image);
        variableId.appendLineNumber(token.beginLine);
    }
    <EQUALS>
    expression()
}

void expression() #void : {}
{	simpleExpression() (
		<EE> simpleExpression() #EE(2)
      | <NE> simpleExpression() #NE(2)
      | <LT> simpleExpression() #LT(2)
      | <LE> simpleExpression() #LE(2)
      | <GT> simpleExpression() #GT(2)
      | <GE> simpleExpression() #GE(2)
	)?
}

void simpleExpression() #void : {}
{
    term() (
        "+" term() #add(2)
      | "-" term() #subtract(2)
      | <OR> term() #or(2)
    )*
}

void term() #void : {}
{
    factor() (
        "*" factor() #multiply(2)
      | "/" factor() #divide(2)
      | <AND> factor() #and(2)
    )*     
}

void factor() #void : {}
{
  (     variable()
	  | integerConstant()
	  | realConstant()
	  | stringConstant()
	  | booleanConstant()
      | "-" factor() #negate(1)
      | "(" expression() ")" 
  )
}

void variable() : {}
{
    <IDENTIFIER> {
        SymTabEntry variableId = symTabStack.lookup(token.image);
        variableId.appendLineNumber(token.beginLine);
        TypeSpec type = variableId.getTypeSpec();
        jjtThis.setTypeSpec(type);
        jjtThis.setAttribute(ID, variableId);
        }
}

void integerConstant() : {}{  <INT_LIT> {
        jjtThis.setTypeSpec(Predefined.integerType);
        jjtThis.setAttribute(VALUE, Integer.parseInt(token.image));
        }}

void realConstant() : {}
{
    <REAL_LIT> {
        jjtThis.setTypeSpec(Predefined.realType);
        jjtThis.setAttribute(VALUE, Float.parseFloat(token.image));
    }
}

void booleanConstant() : {}{	  <TRUE> {}
	| <FALSE> {}
}

void stringConstant() : {}{	<STRING_LIT> {}
} 

void error() #void : {}
{
    <ERROR>
}

JAVACODE
void processVariableDecl(Token token, int index,
                         ArrayList<SymTabEntry> variableList) #void
{
    SymTabEntry variableId = symTabStack.enterLocal(token.image);
    variableId.setIndex(index);
    variableId.setDefinition(DefinitionImpl.VARIABLE);
    variableId.appendLineNumber(token.beginLine);
    variableList.add(variableId);
}

JAVACODE
String handleError(Token token) #void
{
  System.out.println("*** ERROR: Line " + token.beginLine +
                     " after \"" + token.image + "\"");
  
  Token t;
  
  do {
    t = getNextToken();
  } while (t.kind != EOL && t.kind != SEMICOLON);
  
  jjtree.popNode();
  return t.image;
}